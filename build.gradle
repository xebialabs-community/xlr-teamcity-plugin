import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id "java"
  id "com.github.hierynomus.license" version "0.16.1"
  id 'nebula.release' version '19.0.10'
}

// For Java 17
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://dist.xebialabs.com/public/maven2'
  }
}

def apiVersion = "24.1.8"

dependencies {
  implementation "com.xebialabs.deployit:udm-plugin-api:$apiVersion"
  implementation "org.slf4j:slf4j-api:2.0.12"
}

version = "2.1.0"

if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}

processResources.configure {
  filesNotMatching('**/*.png') {
    filter ReplaceTokens, tokens: [
      'project.version': version.toString(),
      'project.name'   : rootProject.name
    ]
  }
}

license {
  header rootProject.file('License.md')
  excludes(["**/*.png", "**/vendor/*.*", "**/docker-compose.yml"])
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'XEBIALABS'
}
